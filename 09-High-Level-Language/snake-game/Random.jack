class Random {

    static int seed;

    function void setSeed(int Aseed) {
        let seed = Aseed;
        return;
    }

    /** Return a random number in the range 0..32767. */
    function int rand() {
        let seed = seed + 20251;
        if (seed < 0) {
            let seed = seed - 32767 - 1;
        }
        return seed;
    }

    /** Return a random number within given range. */
    function int randn(int x, int y) {
        var int mask;
        var int ret;
        let mask = 1;

        while (mask < y) {
            let mask = mask * 2 + 1;
        }
        let ret = Random.rand() & mask;
        while ((ret < x) | (ret > y)) {
            let ret = Random.rand() & mask;
        }
        return ret;
    }

}